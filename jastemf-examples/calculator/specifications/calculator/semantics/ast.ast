/**
 * <copyright>
 *
 * This program and the accompanying materials are made available under the
 * terms of the BSD 3-clause license which accompanies this distribution.
 *
 * </copyright>
 */

/**
 * @author C. BÃ¼rger
 */

CompilationUnit ::= Declaration*;

abstract Statement;
	
	Block:Statement ::= Statement*;
	If:Statement ::= Condition:Expression Body:Block [Alternative:Block];
	While:Statement ::= Condition:Expression Body:Block;
	VariableAssignment:Statement ::= <LValue:String> RValue:Expression;
	ProcedureReturn:Statement ::= [Expression];
	Write:Statement ::= Expression;
	
	abstract Declaration:Statement ::= <Name:String>;
		
		ProcedureDeclaration:Declaration ::=
				Parameter:VariableDeclaration*
				<ReturnType:Type>
				Body:Block;
		VariableDeclaration:Declaration ::=
				<DeclaredType:Type>;
	
	abstract Expression:Statement;
		
		Constant:Expression ::= <Lexem:String>;
		Reference:Expression ::= <Name:String>;
		ProcedureCall:Expression ::= <Name:String> Argument:Expression*;
		NestedExpression:Expression ::= Expression;
		
		abstract UnaryExpression:Expression ::= Operand:Expression;
			
			Not:UnaryExpression;
			UMinus:UnaryExpression;
			
		abstract BinaryExpression:Expression ::=
				Operand1:Expression
				Operand2:Expression;
			
			And:BinaryExpression;
			Or:BinaryExpression;
			Equal:BinaryExpression;
			GreaterThan:BinaryExpression;
			Addition:BinaryExpression;
			Subtraction:BinaryExpression;
			Multiplication:BinaryExpression;
			Division:BinaryExpression;
